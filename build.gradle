buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.20.0'
    classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
  }
}

plugins {
    id 'java'
    id 'jacoco'
    id "com.github.spotbugs" version "2.0.0"
    id 'eclipse'
    id 'idea'
    id 'maven'
    id 'signing'
    id 'com.palantir.circle.style' version '1.1.4'
    id 'net.researchgate.release' version '2.8.0'
}

apply plugin: 'com.bmuschko.nexus'
apply plugin: 'io.codearte.nexus-staging'

ext.nexusUsername = "$System.env.NEXUS_USER"
ext.nexusPassword = "$System.env.NEXUS_PASSWORD"

sourceCompatibility = '1.8'
targetCompatibility = '1.8'
group = 'io.github.clormor'

ext.'signing.keyId' = "$System.env.NEXUS_KEY_ID"
ext.'signing.secretKeyRingFile' = "$System.env.NEXUS_KEY_FILE"
ext.'signing.password' = "$System.env.NEXUS_KEY_PASSWORD"

repositories {
    mavenCentral()
}

dependencies {
    compile "com.google.guava:guava:$guavaVersion"
    compile "com.github.spotbugs:spotbugs-annotations:$spotbugsVersion"
    testCompile "junit:junit:$junitVersion"
}

idea {
    module {
        inheritOutputDirs = true
        downloadJavadoc = true
        downloadSources = true
        scopes.PROVIDED.plus += [configurations.compileOnly]
        iml {
            whenMerged { module ->
                def compileDeps = module.dependencies.findAll { it.scope == 'COMPILE' }
                compileDeps*.exported = true
            }
        }
    }
}

// enable coverage xml reports for codeclimate
jacocoTestReport {
    reports {
        xml.enabled true
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                // set this to a threshold you wish to enforce
                minimum = 0.8
            }
        }
    }
}

// run coverage as part of the build
check.dependsOn jacocoTestReport
check.dependsOn jacocoTestCoverageVerification

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

release {
    git {
        // only perform releases on develop
        requireBranch = 'develop'
        // push releases to a separate release branch
        pushReleaseVersionBranch = 'release'
    }
}

nexusStaging {
    delayBetweenRetriesInMillis = 5000
}

modifyPom {
    project {
        name 'Hacker Rank Java Solutions'
        description 'Contains java solutions for Hacker Rank problems'
        url 'https://github.com/clormor/hacker-rank-java'
        inceptionYear '2019'

        scm {
            url 'https://github.com/clormor/hacker-rank-java'
            connection 'git@github.com:clormor/hacker-rank-java.git'
            developerConnection 'git@github.com:clormor/hacker-rank-java.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'clormor'
                name 'Chris Lormor'
                email 'cdlormor@gmail.com'
            }
        }
    }
}

